var busy = TRUE;
var hideInventory = TRUE;

sub go () {
	onLeftMouse (leftClick);
	onRightMouse (dropObj);
	busy = FALSE;
	onFocusChange (handleFocus);

	# We haven't been updating, so let's do this ourselves
	handleFocus (getOverObject ());
}

sub dropObj () {
	currentInvItem = NULL;
	handleFocus (getOverObject ());
}

sub stop () {
	onFocusChange ();
	statusText ("");
	onLeftMouse ();
	onRightMouse (skipper);
	setCursor (anim ('iface/mouse.duc', 1));
	busy = TRUE;
}

sub skipper () {
	skipSpeech ();
}

sub handleFocus (o) {
	if (o && (o != ego || currentInvItem != NULL)) {
		if (! callEvent (oneCursor, o)) setCursor (anim ('iface/mouse.duc', 2));
	} else {
		setCursor (anim ('iface/mouse.duc', 0));
	}
	drawStatusLine (o);
}

sub drawStatusLine (overObject) {
	if (! busy) {
		if (currentInvItem) {
			if (overObject && overObject != currentInvItem) {
				statusText ("Use " + currentInvItem + " with " + overObject);
			} else {
				statusText ("Use " + currentInvItem + " with");
			}				
		} else {
			statusText ((overObject && overObject != ego && currentInvItem == NULL ? overObject : "") + ((overObject != cancel) ? (thisObject ? thisObject : "") : ""));
		}
	}
}

# Fading scripts
sub fadeIn()
{
	transitionMode(FADE);
	for(var a = 0; a < 256; a += 10)
	{
		transitionLevel(a);
		pause(1);
	}
	transitionLevel(256);
}

sub fadeOut()
{
	transitionMode(FADE);
	for(var a = 255; a > 0; a -= 10)
	{
		transitionLevel(a);
		pause(1);
	}
	transitionLevel(0);
}

sub smoothScroll(startX, x)
{
	var camX = startX;
	
	while((x > startX) ? (camX < x) : (camX > x))
	{
		camX += (x - startX) / 45;
		aimCamera(camX, 384);
		pause(1);
	}
}

var currentRoom = NULL;
var lastRoom = NULL;

sub gotoRoom (r) {
	# Get rid of all the object types
	removeAllCharacters ();
	removeAllScreenRegions ();

	# Get rid of the floor and z-buffer, in case the new room forgets to load any
	setFloor (NULL);
	setZBuffer (NULL);
	
	# Finish any running timers we might have on the go
	completeTimers ();

	# Fix the variables lastRoom and currentRoom
	# (We don't NEED them, but they are useful in scripts)
	lastRoom = currentRoom;
	currentRoom = r;

	if(hideInventory == FALSE)
	{
		addCharacter (inventoryIcon, 20, 755, anim ('iface/mouse.duc', 5));
		setCharacterExtra (inventoryIcon, ICON);
	}

	# Now, finally call the function to build the new room
	r ();
}
