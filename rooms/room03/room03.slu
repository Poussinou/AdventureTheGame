var room03_part = "a";
var room03_centerX = 683;

sub room03()
{
	setSceneDimensions(2724, 768);
	addOverlay('rooms/room03/room03.png', 0, 0);
	setFloor('rooms/room03/room03.flo');
	
	setScale(450, 420);
	
	addCharacter(ego, 87, 670, makeEgoAnim());
	
	addCharacter(room03_broom, 1860, 580, anim('rooms/room03/broom.duc', 0));
	setCharacterExtra(room03_broom, FIXEDSIZE);

	addScreenRegion(room03_door, 2627, 419, 2698, 703, 2608, 697, EAST);
	addScreenRegion(room03_abTransition, 1320, 0, 1420, 768, 0, 0, 0);
	addScreenRegion(room03_posterFarm, 745, 87, 1012, 420, 866, 616, NORTH);

	setCharacterWalkSpeed(ego, 8);
}

sub gotoRoom03Part(part, x)
{
	room03_part = part;
	smoothScroll(room03_centerX, x);
	room03_centerX = x;
}

objectType room03_door("door")
{
	event oneCursor = arrowEast;
	event onlyAction
	{
		gotoRoom(room05);
	}
}

objectType room03_abTransition("go")
{
	event oneCursor = arrowEast;
	event onlyAction
	{
		if(room03_part == "a")
		{
			moveCharacter(ego, 1430, getObjectY(ego));
			gotoRoom03Part("b", 2049);
			
		}
		else if(room03_part == "b")
		{
			moveCharacter(ego, 1310, getObjectY(ego));
			gotoRoom03Part("a", 683);
		}
	}
}

objectType room03_broom("broom")
{
	event lookAt
	{
		say(ego, "What a slick broom.");
	}
	
	event interactWith
	{
		say(ego, "I'll take it.");
		
		pushToStack(inventory, item_broom);
		
		removeCharacter(room03_broom);
	}
}

objectType room03_posterFarm("poster")
{
	event lookAt
	{
		say(ego, "I'm Max Burton and this is my favorite game on this wall!");
	}
	
	event default
	{
		say(ego, pickOne("I like the design.", "I have to admit - idea for this game is great!"));
	}
}


