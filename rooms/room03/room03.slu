var room03_part = "a";
var room03_centerX = 683;

var room03_idTaken = FALSE;

var room03_broomTaken = FALSE;

var straightFromBoss = FALSE;

sub room03()
{
	room03_part = "a";
	room03_centerX = 683;

	setSceneDimensions(2724, 768);
	addOverlay('rooms/room03/room03.png', 0, 0);
	
	if (afterPrison == FALSE) {
		addOverlay('rooms/room03/room03open.png', 0, 0);
	} else {
		addCharacter(room03_chuck, 2527, 419, anim('rooms/room02/book.duc', 0));
		setCharacterExtra(room03_chuck, FIXEDSIZE);
		
	}
	
	setFloor('rooms/room03/room03.flo');
	
	setScale(450, 420);
	
	if (straightFromBoss == FALSE) {
		addCharacter(ego, 87, 670, makeEgoAnim());
		turnCharacter(ego, EAST);
	} else {
		addCharacter(ego, 2608, 697, makeEgoAnim());
		turnCharacter(ego, WEST);
		room03_part = "b";
		room03_centerX = 2049;
		aimCamera(room03_centerX, 384);
		
	}
	straightFromBoss = FALSE;
	
	if (room03_broomTaken == FALSE) {
		addCharacter(room03_broom, 1860, 580, anim('rooms/room03/broom.duc', 0));
		setCharacterExtra(room03_broom, FIXEDSIZE);
	}

		
	addScreenRegion(room03_door, 2627, 419, 2698, 703, 2608, 697, EAST);
	addScreenRegion(room03_exit, 0, 768, 200, 768, 0, 768, WEST);
	addScreenRegion(room03_abTransition, 1320, 0, 1420, 768, 0, 0, 0);
	addScreenRegion(room03_posterFarm, 745, 87, 1012, 420, 866, 616, NORTH);

	setCharacterWalkSpeed(ego, 8);
	
}

sub gotoRoom03Part(part, x)
{
	room03_part = part;
	smoothScroll(room03_centerX, x);
	room03_centerX = x;
}

objectType room03_door("door")
{
	event oneCursor = arrowEast;
	event onlyAction
	{
		if (afterPrison == TRUE) {
			say(ego, "It's locked.");
		} else {
			gotoRoom(room05);
		}
	}
	
	event item_bossKey {
		say(ego, "It matches!");
		gotoRoom(room05);
	}
}

objectType room03_exit("elevator")
{
	event oneCursor = arrowEast;
	event onlyAction
	{
		gotoRoom(roomElevator);
	}
}


objectType room03_chuck("chuck the plant") {
	event lookAt {
		say(ego, "What a nice plant!");
	}
	event interactWith {
		if (room03_idTaken == FALSE) {
			narratorSay("Max touches the plant.");
			say(ego, "Hey, there's a key behind it!");
			turnCharacter(ego, WEST);
			pause(10);
			turnCharacter(ego, EAST);
			pause(10);
			turnCharacter(ego, NORTH);
			say(ego, "I'll just lend it for a while.");
			pushToStack(inventory, item_bossKey);

			#pushToStack(inventory, item_bossIDcard);
			
			room03_idTaken = TRUE;
		} else {
			narratorSay("Max touches the plant with great love and care.");
			narratorSay("Plant seems to be happy.");
		}
		
	}
}

objectType room03_abTransition("go")
{
	event oneCursor = arrowEast;
	event onlyAction
	{
		if(room03_part == "a")
		{
			moveCharacter(ego, 1430, getObjectY(ego));
			gotoRoom03Part("b", 2049);
			
		}
		else if(room03_part == "b")
		{
			moveCharacter(ego, 1310, getObjectY(ego));
			gotoRoom03Part("a", 683);
		}
	}
}

objectType room03_broom("broom")
{
	event lookAt
	{
		say(ego, "What a slick broom.");
	}
	
	event interactWith
	{
		say(ego, "I'll take it.");
		
		pushToStack(inventory, item_broom);
		
		removeCharacter(room03_broom);
		
		room03_broomTaken = TRUE;
	}
}

objectType room03_posterFarm("poster")
{
	event lookAt
	{
		say(ego, "I'm Max Burton and this is my favorite game on this wall!");
	}
	
	event default
	{
		say(ego, pickOne("I like the design.", "I have to admit - idea for this game is great!"));
	}
}


