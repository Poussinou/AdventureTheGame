var room03_part = "a";
var room03_centerX = 683;

var room03_chuckBroken = FALSE;
var room03_idTaken = FALSE;

var room03_broomTaken = FALSE;

sub room03()
{
	setSceneDimensions(2724, 768);
	addOverlay('rooms/room03/room03.png', 0, 0);
	setFloor('rooms/room03/room03.flo');
	
	setScale(450, 420);
	
	addCharacter(ego, 87, 670, makeEgoAnim());
	
	if (room03_broomTaken == FALSE) {
		addCharacter(room03_broom, 1860, 580, anim('rooms/room03/broom.duc', 0));
		setCharacterExtra(room03_broom, FIXEDSIZE);
	}

	if (room03_chuckBroken) {
		# rysowanie martwego chucka
		if (room03_idTaken == FALSE) {
			# rysowanie idka
		}
	} else {
		addCharacter(room03_chuck, 800, 645, anim('rooms/room02/book.duc', 0));
		setCharacterExtra(room03_chuck, FIXEDSIZE);
	}
		
	addScreenRegion(room03_door, 2627, 419, 2698, 703, 2608, 697, EAST);
	addScreenRegion(room03_abTransition, 1320, 0, 1420, 768, 0, 0, 0);
	addScreenRegion(room03_posterFarm, 745, 87, 1012, 420, 866, 616, NORTH);

	setCharacterWalkSpeed(ego, 8);
	
	room03_part = "a";
}

sub gotoRoom03Part(part, x)
{
	room03_part = part;
	smoothScroll(room03_centerX, x);
	room03_centerX = x;
}

objectType room03_door("door")
{
	event oneCursor = arrowEast;
	event onlyAction
	{
		if (afterPrison) {
			say(ego, "It's locked.");
		} else {
			gotoRoom(room05);
		}
	}
}

objectType room03_chuck("chuck the plant") {
	event lookAt {
		say(ego, "What a nice plant!");
	}
	event interactWith {
		say(ego, "Whooops!");
		narratorSay("CHuck breaks");
		think(room03_chuck, "Oh no, not again...");
		narratorSay("wild ID arrives");
		
		pushToStack(inventory, item_bossIDcard);
		
		room03_chuckBroken = TRUE;
		room03_idTaken = FALSE;
		
		
	}
}

objectType room03_abTransition("go")
{
	event oneCursor = arrowEast;
	event onlyAction
	{
		if(room03_part == "a")
		{
			moveCharacter(ego, 1430, getObjectY(ego));
			gotoRoom03Part("b", 2049);
			
		}
		else if(room03_part == "b")
		{
			moveCharacter(ego, 1310, getObjectY(ego));
			gotoRoom03Part("a", 683);
		}
	}
}

objectType room03_broom("broom")
{
	event lookAt
	{
		say(ego, "What a slick broom.");
	}
	
	event interactWith
	{
		say(ego, "I'll take it.");
		
		pushToStack(inventory, item_broom);
		
		removeCharacter(room03_broom);
		
		room03_broomTaken = TRUE;
	}
}

objectType room03_posterFarm("poster")
{
	event lookAt
	{
		say(ego, "I'm Max Burton and this is my favorite game on this wall!");
	}
	
	event default
	{
		say(ego, pickOne("I like the design.", "I have to admit - idea for this game is great!"));
	}
}


