var room03_part = "a";
var room03_centerX = 683;

var room03_idTaken = FALSE;

var room03_broomTaken = FALSE;

var straightFromBoss = FALSE;

var room03_doorOpened = FALSE;

sub room03()
{
	room03_part = "a";
	room03_centerX = 683;

	setSoundVolume('rooms/room05/muzyczka.wav', 50);
	
	setSceneDimensions(2724, 768);
	addOverlay('rooms/room03/room03.png', 0, 0);

	if (afterPrison == FALSE || room03_doorOpened == TRUE) {
		addOverlay('rooms/room03/room03open.png', 0, 0);
	} else {
		addCharacter(room03_chuck, 2500, 589, anim('rooms/room03/plant.duc', 0));
		setCharacterExtra(room03_chuck, FIXEDSIZE + RECTANGULAR);
		
	}
	
	setFloor('rooms/room03/room03.flo');
	
	setScale(200, 800);
		
	if (room03_broomTaken == FALSE) {
		addCharacter(room03_broom, 1960, 580, anim('rooms/room03/broom.duc', 0));
		setCharacterExtra(room03_broom, FIXEDSIZE);
	}
	
	
	addScreenRegion(room03_door, 2627, 419, 2698, 703, 2608, 697, EAST);
	addScreenRegion(room03_exit, 0, 0, 200, 768, 0, 768, WEST);
	addScreenRegion(room03_abTransition, 1320, 0, 1420, 768, 0, 0, 0);
	addScreenRegion(room03_posterVikings, 200, 90, 485, 445, 340, 616, NORTH);
	addScreenRegion(room03_posterFarm, 745, 87, 1012, 420, 866, 616, NORTH);
	addScreenRegion(room03_posterCows, 1540, 90, 1850, 450, 1695, 616, NORTH);
	addScreenRegion(room03_posterBrush, 2070, 66, 2375, 432, 2222, 616, NORTH);
	
	if (straightFromBoss == FALSE) {
		if (muzyczkaPlays) {
			loopSound('rooms/room05/muzyczka.wav', 1);
			setSoundVolume('rooms/room05/muzyczka.wav', 15);
		}
		
		addCharacter(ego, -50, 670, makeEgoAnim());
		setCharacterWalkSpeed(ego, 8);
		moveCharacter(ego, 87, 670);
		turnCharacter(ego, EAST);
	} else {
		addCharacter(ego, 2608, 697, makeEgoAnim());
		setCharacterWalkSpeed(ego, 8);
		turnCharacter(ego, WEST);
		room03_part = "b";
		room03_centerX = 2049;
		aimCamera(room03_centerX, 384);
	}
	
	if (DEBUG) {
		pushToStack(inventory, item_bossKey);
	}
	
	straightFromBoss = FALSE;
	
}

sub gotoRoom03Part(part, x)
{
	room03_part = part;
	smoothScroll(room03_centerX, x);
	room03_centerX = x;
}

objectType room03_door("door")
{
	event oneCursor = arrowEast;
	event onlyAction
	{
		if (afterPrison == TRUE && room03_doorOpened == FALSE) {
			say(ego, "It's locked.");
		} else {
			gotoRoom(room05);
		}
	}
	
	event item_bossKey {
		if (! room03_doorOpened) {
			say(ego, "It matches!");
		}
		room03_doorOpened = TRUE;
		gotoRoom(room05);
	}
}

objectType room03_exit("elevator")
{
	event oneCursor = arrowEast;
	event onlyAction
	{
		forceCharacter(ego, -100, 768);
		stopSound('rooms/room05/muzyczka.wav');
		gotoRoom(roomElevator);
	}
}


objectType room03_chuck("chuck the plant") {
	event lookAt {
		say(ego, "What a nice plant!");
	}
	event interactWith {
		if (room03_idTaken == FALSE) {
			say(ego, "Hey, there's a key behind it!");
			pause(10);
			turnCharacter(ego, WEST);
			pause(30);
			turnCharacter(ego, EAST);
			pause(35);
			turnCharacter(ego, NORTH);
			pause(10);
			say(ego, "I'll just lend it for a while.");
			pushToStack(inventory, item_bossKey);
			
			room03_idTaken = TRUE;
		} else {
			narratorSay("Max touches the plant with great love and care.");
			narratorSay("Plant seems to be happy.");
		}
		
	}
	event walkToPerson
	{
		moveCharacter (ego, 2500, 630);
		turnCharacter (ego, NORTH);
	}
}

objectType room03_abTransition("go")
{
	event oneCursor = arrowEast;
	event onlyAction
	{
		if(room03_part == "a")
		{
			if (DEBUG) {
				jumpCharacter(ego, 1430, getObjectY(ego));
			} else {
				moveCharacter(ego, 1430, getObjectY(ego));
			}
			setSoundVolume('rooms/room05/muzyczka.wav', 50);
			gotoRoom03Part("b", 2049);
			
		}
		else if(room03_part == "b")
		{
			if (DEBUG) {
				jumpCharacter(ego, 1310, getObjectY(ego));
			} else {
				moveCharacter(ego, 1310, getObjectY(ego));
			}
			setSoundVolume('rooms/room05/muzyczka.wav', 15);
			gotoRoom03Part("a", 683);
		}
	}
}

objectType room03_broom("broom")
{
	event lookAt
	{
		say(ego, "What a slick broom.");
	}
	
	event interactWith
	{
		say(ego, "I'll take it.");
		
		pushToStack(inventory, item_broom);
		
		removeCharacter(room03_broom);
		
		room03_broomTaken = TRUE;
	}
	
	event walkToPerson
	{
		moveCharacter (ego, 1960, 650);
		turnCharacter (ego, NORTH);
	}
}

objectType room03_posterVikings("poster")
{
	event lookAt
	{
		say(ego, "Seems like they haven't updated this wall for ages. There's already Vikings XXI out there!");
		say(ego, "Not as fun as Vikings XX, though.");
	}
	
	event interactWith
	{
		say(ego, "Nah, I didn't really like this one.");
		say(ego, "If it was Vikings XVI though...");
	}
}

objectType room03_posterFarm("poster")
{
	event lookAt
	{
		say(ego, "I'm Max Burton and this is my favorite game on this wall!");
	}
	
	event interactWith
	{
		say(ego, "I already have one in my bedroom.");
	}
}

objectType room03_posterCows("poster")
{
	event lookAt
	{
		say(ego, "I never really got that Angry Cows craze... yet this Inception Extension sounds like it could be fun!");
	}
	
	event interactWith
	{
		say(ego, "No, thanks, I don't need an Angry Cows poster to feel fulfilled.");
	}
	
	event default
	{
		say(ego, "I don't think cows would like that.");
	}
}

objectType room03_posterBrush("poster")
{
	event lookAt
	{
		say(ego, "\"My name is Brush Guy Treepweed and I wanna be a painter!\" Haha, this guy's hilarious!");
		say(ego, "The sole concept of Insult Paint Mixing is still cracking me up!");
	}
	
	event interactWith
	{
		say(ego, "I love Brush Guy, but that would be stealing.");
	}
}


