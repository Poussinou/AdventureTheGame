var vol = 255;
sub elevatorMusicDown() {
	vol = 255;
	loop {
		setSoundVolume('music/1.ogg', vol);
		vol=vol-2;
		pause(1);
		if (vol < 40) return;
	}
}

var vol2 = 100;
sub elevatorMusicUp() {
	vol2 = 70;
	loop {
		setSoundVolume('music/1.ogg', vol);
		setSoundVolume('music/2.ogg', vol2);
		vol=vol+5;
		if (vol2 > 0) {
			vol2 = vol2-5;
		}
		pause(1);
		if (vol >= 255) {
			setSoundVolume('music/1.ogg', 255);
			stopSound('music/2.ogg');
			return;
		}
	}
}

sub roomElevatorIntro()
{
	# first things first
	spawnSub(elevatorMusicDown);
	setSceneDimensions(1366, 768);
	addOverlay('rooms/roomElevator/roomElevator.png', 0, 0);
	setFloor('rooms/roomElevator/roomElevator.flo');
	
	setScale(50, 550);
	
	addCharacter (ego, 580, 550, makeEgoAnim());
	setCharacterWalkSpeed(ego, 10);
	
	forceCharacter(ego, 450, 840);
	turnCharacter (ego, WEST);

	addScreenRegion(roomElevatorIntro_notification, 150, 123, 355, 200, 0, 0, 0);
	
	pause(10);

	playSound('rooms/roomElevator/elevator.ogg');
	
	pause(40);
	
	playSound('rooms/roomElevator/buzzbg.ogg');

	
	addCharacter(roomElevatorIntro_door, 0, 0, makeStaticCostume(
		anim('rooms/roomElevator/door.duc', wait(0, 16), wait(1, 4), wait(2,8), wait(1, 12), wait(1, 4), wait(2,8), wait(1, 12), wait(0, 20))
	));
	setCharacterExtra(roomElevatorIntro_door, FIXEDSIZE);
	
	pause(40);
	turnCharacter (ego, NORTH);
	
        loopSound('music/2.ogg');
        setSoundVolume('music/2.ogg', 70);
	pause(60);
	
	onRightMouse (skipElevatorMovie);
	onKeyboard();
	playMovie('rooms/roomElevator/elevator.webm');
	onKeyboard(keys);
	onRightMouse (skipper);
	
	removeCharacter(roomElevatorIntro_door);
	
	# play sound ding
	playSound('rooms/roomElevator/ding.ogg');

	pause(15);
	playSound('rooms/roomElevator/elevatoropen.ogg');
	pause(25);
	
	spawnSub(elevatorMusicUp);

	say(ego, "Neat!");
	
	pause(10);
	
	moveCharacter(ego, 650, 0);
	setCharacterWalkSpeed(ego, 5);
	
	roomElevator_floor = 2;
	
	gotoRoom(room02);
}

sub skipElevatorMovie() {
	stopMovie();
	stopSound('rooms/roomElevator/elevator.ogg');
	stopSound('rooms/roomElevator/buzzbg.ogg');
}
objectType roomElevatorIntro_door("")
{
}

objectType roomElevatorIntro_notification("")
{
	speechColor 150, 150, 150;
}

