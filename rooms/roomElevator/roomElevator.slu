var roomElevator_floor = 1;

sub roomElevator()
{
	# first things first
	setSceneDimensions(1366, 768);
	addOverlay('rooms/roomElevator/roomElevator.png', 0, 0);
	setFloor('rooms/roomElevator/roomElevator.flo');
	
	setScale(50, 550);
	
	addCharacter (ego, 580, 550, makeEgoAnim());
	setCharacterWalkSpeed(ego, 10);
	
	addScreenRegion(roomElevator_notification, 150, 123, 355, 200, 0, 0, 0);
	
	forceCharacter(ego, 450, 840);
	turnCharacter (ego, WEST);
	
	pause(10);

	say(roomElevator_notification, "To which floor do you wish to go?");
	
	if (DEBUG) {
		addSpeechChoice("Intro (DEBUG)", 5);
	}
	addSpeechChoice("Reception", 1);
	addSpeechChoice("Software Development Room", 2);
	if (DEBUG || room01_talkedToRebels == TRUE) {
		addSpeechChoice("CEO Office" + ((! room01_talkedToRebels) ? " (DEBUG)" : ""), 3);
	}
	if (DEBUG || afterPrison == TRUE) {
		addSpeechChoice("Basement" + ((! afterPrison) ? " (DEBUG)" : ""), 4);
	}
	if (DEBUG) {
		addSpeechChoice("Prison (DEBUG)", 6);
		addSpeechChoice("CEO room (DEBUG)", 7);
		addSpeechChoice("SECRET room (DEBUG)", 8);
	}
	
	roomElevator_floor = getSpeechChoice(NULL);
	
	var text = "You have arrived at ", location, room;
	
	if(roomElevator_floor == 1)
	{
		location = "reception";
		room = room01;
	}
	else if(roomElevator_floor == 2)
	{
		location = "Software Development Room";
		room = room02;
	}
	else if(roomElevator_floor == 3)
	{
		location = "CEO Office";
		room = room03;
	}
	else if(roomElevator_floor == 4)
	{
		location = "basement";
		room = room07;
	}
	else if(roomElevator_floor == 5)
	{
		location = "this game's intro";
		room = intro;
	}
	else if(roomElevator_floor == 6)
	{
		location = "prison";
		room = room04;
	}
	else if(roomElevator_floor == 7)
	{
		location = "CEO room";
		room = room05;
	}
	else if(roomElevator_floor == 8)
	{
		location = "SECRET room";
		room = room06;
	}
	turnCharacter (ego, NORTH);
	
	if (! DEBUG) {
		playSound('rooms/roomElevator/elevator2.wav');
		pause(200);
		
		playSound('rooms/roomElevator/ding.wav');
		stopSound('rooms/roomElevator/elevator2.wav');
	}
	
	say(roomElevator_notification, text + location + ".");
	
	moveCharacter(ego, 650, 0);
	setCharacterWalkSpeed(ego, 5);
	
	gotoRoom(room);
}


objectType roomElevator_notification("")
{
	speechColor 150, 150, 150;
}

