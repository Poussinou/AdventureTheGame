var roomElevator_floor = 1;

sub roomElevator()
{
	# first things first
	setSceneDimensions(1366, 768);
	addOverlay('rooms/roomElevator/roomElevator.png', 0, 0);
	setFloor('rooms/roomElevator/roomElevator.flo');
	
	setScale(50, 550);
	
	addCharacter (ego, 580, 550, makeEgoAnim());
	setCharacterWalkSpeed(ego, 10);
	
	addScreenRegion(roomElevator_notification, 150, 123, 355, 200, 0, 0, 0);
	
	#if(roomElevator_floor == 1)
	#	roomElevator_floor = 2;
	#else if(roomElevator_floor == 2)
	#	roomElevator_floor = 1;
	
	forceCharacter(ego, 450, 840);
	turnCharacter (ego, WEST);
	
	pause(10);

	say(roomElevator_notification, "To which floor do you wish to go?");
	
	if(roomElevator_floor != 1)
		addSpeechChoice("Reception", 1);
	if(roomElevator_floor != 2)
		addSpeechChoice("Software Development Room", 2);
	if(roomElevator_floor != 3 && room01_talkedToRebels == TRUE)
		addSpeechChoice("CEO Office", 3);
	if(roomElevator_floor != 4 && afterPrison == TRUE)
		addSpeechChoice("Basement", 4);
		
	roomElevator_floor = getSpeechChoice(NULL);
	
	var text = "You have arrived at ", location, room;
	
	if(roomElevator_floor == 1)
	{
		location = "Reception";
		room = room01;
	}
	else if(roomElevator_floor == 2)
	{
		location = "Software Development Room";
		room = room02;
	}
	else if(roomElevator_floor == 3)
	{
		location = "CEO Office";
		room = room05;
	}
	else if(roomElevator_floor == 4)
	{
		location = "Basement";
		room = room06;
	}
	
	playSound('rooms/roomElevator/elevator2.wav');
	pause(200);
	
	playSound('rooms/roomElevator/ding.wav');
	stopSound('rooms/roomElevator/elevator2.wav');
	
	say(roomElevator_notification, text + location + ".");
	
	
	moveCharacter(ego, 650, 0);
	setCharacterWalkSpeed(ego, 5);
	
	gotoRoom(room);
}




objectType roomElevator_notification("")
{
	speechColor 100, 100, 100;
}

