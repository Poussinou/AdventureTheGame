var room04_lookedAtDoor = FALSE;

var room04_scratched = FALSE;

var afterPrison = FALSE;

var straightFromPrison = FALSE;

sub bossMusicDown() {
        vol = 255;
        loop {
                setSoundVolume('music/3.ogg', vol);
                vol=vol-5;
                pause(1);
                if (vol < 0) {
                        stopSound('music/3.ogg');
                        loopSound('music/5.ogg');
                        return;
                }
        }
}

sub room04()
{
	setSceneDimensions(1366, 768);
	addOverlay('rooms/room04/room04.png', 0, 0);
	setFloor('rooms/room04/room04.flo');
	
	spawnSub(bossMusicDown);

	setScale(400, 380);
	
	addCharacter(ego, 450, 670, makeEgoAnim());

	addScreenRegion(room04_door, 1068, 271, 1216, 726, 1057, 709, EAST);

	addScreenRegion(room04_window, 530, 225, 740, 360, 630, 630, NORTH);
	
	addScreenRegion(room04_scratches, 690, 370, 775, 430, 705, 630, NORTH);

	addCharacter(room04_potato, 230, 730, makeStaticCostume(anim ('rooms/room04/potato.duc', 0)));
	setCharacterExtra(room04_potato, FIXEDSIZE);
	
	setCharacterWalkSpeed(ego, 8);
	
	fadeIn();
	
	say(ego, "What kind of company has its very own prison inside their headquarters?");

	straightFromPrison = TRUE;
	
	if (DEBUG) {
		pushToStack(inventory, item_picklock);
	}
	
}

objectType room04_door("prison door")
{
	event lookAt
	{
		say(ego, "Very heavy door.");
		pause(40);
		say(ego, "Hey, it has traditional keyhole instead of ID scanner.");
		say(ego, "If only I could make some kind of picklock...");
		
		room04_lookedAtDoor = TRUE;
	}
	
	event interactWith
	{
		say(ego, "*knock* *knock*");
		pause(90);
		say(ego, "I guess nobody is here.");
	}
	
	event item_picklock {
		deleteFromStack(inventory, item_picklock);
		afterPrison = TRUE;
		playSound('door.ogg');
		gotoRoom(room07);
	}
	
}
objectType room04_picklock("picklock")
{
}

objectType room04_window("window bars")
{
	event lookAt
	{
		say(ego, "Being there would be a bit more bearable if only there wasn't a wall right behind that window...");
	}
	event interactWith
	{
		say(ego, "These bars have edges so sharp I could slice a cake with it.");
	}
	event item_potato
	{
		if (room04_lookedAtDoor == TRUE) {
			say(ego, "Hmm... I've got an idea that's so stupid I really want to try it.");
			narratorSay("Max uses strangely sharp window bars to scrape a picklock out of the potato.");
			deleteFromStack(inventory, item_potato);
			pushToStack(inventory, item_picklock);
			say(ego, "Having been a scout sure does pay off!");
		} else {
			say(ego, "I could scrape something out of the potato with these bars.");
			say(ego, "But what?");
		}
	}
}

objectType room04_scratches("scratches")
{
	event lookAt
	{
		say(ego, "Someone must have been a very precious resource to the company.");
	}
	event interactWith
	{
		if (! room04_scratched) {
			playSound('rooms/room04/scratch.ogg');
			addOverlay('rooms/room04/scratch.png', 0, 0);
			room04_scratched = TRUE;
		} else {
			say(ego, "Enough of scratching walls, let's get out of here.");
		}
	}
}

objectType room04_potato("potato")
{
	event lookAt
	{
		say(ego, "It's a potato.");
		say(ego, "I wonder what has it done to be locked in prison.");
	}
	event interactWith
	{
		say(ego, "I'm not hungry right now, I'll save it for later.");
		pushToStack(inventory, item_potato);
		removeCharacter(room04_potato);
	}
	
	event walkToPerson
	{
		moveCharacter (ego, 230, 700);
		turnCharacter (ego, SOUTH);
	}
}
