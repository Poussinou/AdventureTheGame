

var lookedAtCard = FALSE;
var pickedUpCard = FALSE;
var room05_drawerLooked = FALSE;
var muzyczkaPlays = FALSE;
var muzyczkaPlayed = FALSE;
	
sub room05()
{
	setSceneDimensions(1366, 768);
	addOverlay('rooms/room05/room05.png', 0, 0);
	setFloor('rooms/room05/room05.flo');
	
	setScale(290, 490);
	
	setSoundVolume('rooms/room05/muzyczka.wav', 255);
	
	addCharacter(ego, 91, 660, makeEgoAnim());
	
	lookedAtCard = FALSE;
	
	if (afterPrison == TRUE) {
		if (pickedUpCard == FALSE) {
			addCharacter(room05_bossIDcard, 824, 675, makeStaticCostume(anim('rooms/room05/card.duc', 0)));
			
			addScreenRegion(room05_radio, 804, 425, 904, 460, 750, 650, EAST);
			
		}
	} else {
	
		addCharacter(room05_director, 774, 625, costume(
				anim('rooms/room05/director.duc', wait(0, 4), wait(1, 4)),
				anim('rooms/room05/director.duc', wait(0, 4), wait(1, 4)),
				anim('rooms/room05/director.duc', wait(0, 4), wait(1, 4)),
				anim('rooms/room05/director.duc', wait(0, 4), wait(1, 4)),
				
				NULL, NULL, NULL, NULL,
				
				anim('rooms/room05/director.duc', wait(0, 4), wait(2, 4)),
				anim('rooms/room05/director.duc', wait(0, 4), wait(2, 4)),
				anim('rooms/room05/director.duc', wait(0, 4), wait(2, 4)),
				anim('rooms/room05/director.duc', wait(0, 4), wait(2, 4))
			));
	
	}
	
	addScreenRegion(room05_drawer1, 200, 430, 302, 465, 236, 586, NORTH);
	addScreenRegion(room05_drawer2, 200, 466, 302, 517, 236, 586, NORTH);
	addScreenRegion(room05_closet, 85, 221, 187, 530, 140, 618, NORTH);
	addScreenRegion(room05_safe, 434, 231, 557, 382, 505, 603, NORTH);
	addScreenRegion(room05_exit, 0, 0, 50, 768, 75, 670, WEST);	
	
	setCharacterWalkSpeed(ego, 8);
	turnCharacter(ego, EAST);
}

objectType room05_bossIDcard("ID card") {
	event lookAt {
		lookedAtCard = TRUE;
		say(ego, "Hey, it CEO's ID! He must have dropped it when leaving!");
	}
	event interactWith {
		if (lookedAtCard == FALSE) {
			say(ego, "Hey, it CEO's ID! He must have dropped it when leaving!");
		}
		pushToStack(inventory, item_bossIDcard);
		
		removeCharacter(room05_bossIDcard);
		
		say(ego, "Now I can feel great power and no responsibility!");
		
		pickedUpCard = TRUE;
	}
	
	event walkToPerson
	{
		moveCharacter (ego, 700, 690);
		turnCharacter (ego, EAST);
	}
}

objectType room05_drawer1("upper drawer")
{
	event interactWith
	{
		if (afterPrison == TRUE) {
			say(ego, "Nothing interesting here.");
		} else {
			if (room05_drawerLooked) {
				say(ego, "I'm not putting my hands into this drawer again!");
			} else {
				say(ego, "Let's see...");
				turnCharacter(ego, NORTH);
				pause(60);
				say(ego, "Well, he sure does have some dirty secrets... But I doubt these are the ones I'm interested in.");
				pause(30);
				say(ego, "There's something! The secret of...");
				say(ego, "...Monkey Island. I don't think that's it.");
				pause(25);
				say(ego, "Oh! Some secret blueprints... For a coffee machine?");
				pause(40);
				say(ego, "Ouch... I think something bit me.");
				room05_drawerLooked = TRUE;
			}
		}
	}
}

objectType room05_drawer2("lower drawer")
{
	event interactWith
	{
		say(ego, "It's so full it won't even open.");
	}
}

objectType room05_closet("closet")
{
	event interactWith
	{
		say(ego, "I don't want to. I think I've heard bones rattling inside.");
	}
	
	event lookAt
	{
		say(ego, "It's an ordinary closet.");
	}
}

objectType room05_radio("old radio")
{
	event interactWith
	{
		turnCharacter(ego, EAST);
		if (muzyczkaPlays == FALSE) {
			loopSound('rooms/room05/muzyczka.wav', 1);
			if (muzyczkaPlayed == FALSE) {
				pause(450);
				say(ego, "Wow...catchy");
				muzyczkaPlayed = TRUE;
			}
			muzyczkaPlays = TRUE;
		} else {
			stopSound('rooms/room05/muzyczka.wav');
			muzyczkaPlays = FALSE;
		}
	}
}

objectType room05_safe("safe")
{
	event interactWith
	{
		if (afterPrison == TRUE) {
			say(ego, "This is not the secret I'm looking for.");
		} else {
			
		pause(30);
		say(room05_director, "What are you doing?");
		turnCharacter(ego, EAST);
		pause(20);
		say(ego, "Uhm... I'm selling these fine leather jackets!");
		say(room05_director, "Ah, digging for secrets, I see?");
		say(room05_director, "Well, these are not the secrets you are looking for.");
		say(room05_director, "Argh.");
		say(room05_director, "My score was over 9000 and I've lost my focus because of you!");
		pause(30);
		say(room05_director, "Security!");
		pause(5);
		addCharacter(room05_security, 112, 650, anim('rooms/room05/security.duc', 0));
		setCharacterExtra(room05_security, FIXEDSIZE);
		turnCharacter(ego, WEST);
		say(room05_security, "You're coming with me, son.");
		
		fadeOut();
		pause(25);
		gotoRoom(room04);
		}
	}
	
	event lookAt
	{
		say(ego, "I'll be damned if this safe didn't contain the Secret.");
	}
}

objectType room05_exit("")
{
	event oneCursor = arrowEast;
	event onlyAction
	{
		straightFromBoss = TRUE;
		
		gotoRoom(room03);
	}
}

objectType room05_director("CEO")
{
	speechColor 255, 255, 255;
	
	event lookAt
	{
		say(ego, "He seems completely absorbed by new Angry Cows expansion.");
	}
	
	event interactWith
	{
		say(ego, "Hello?");
		say(room05_director, "Go away. I'm trying to beat the Kypax Dream level on expert difficulty.");
		say(room05_director, "I'm the 9th player on the highscore list.");
		say(ego, "Shouldn't you be running the company?");
		say(room05_director, "I'm betatesting.");
		say(room05_director, "Yeah.");
		say(room05_director, "That's right.");		
	}
	
	event walkToPerson
	{
		moveCharacter (ego, 566, 639);
		turnCharacter (ego, EAST);
	}
}

objectType room05_security("security guard")
{
	speechColor 255, 255, 255;	
}


